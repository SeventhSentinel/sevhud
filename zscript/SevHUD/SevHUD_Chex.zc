//===========================================================================
//
// SevHUD for Chex Quest
//
// ZScript code: Copyright (C) 2020 - 2025 Nash Muhandes
// Modifications: Copyright (C) 2025 SeventhSentinel
//
// License: MIT
//
//===========================================================================

class SevHUD_Chex : DoomStatusBar
{
	const X_PADDING = 8;

	HUDFont mHUDSmallFont;
	HUDFont mHUDConFont;

	HUDFont mChexIndexFont;

	Actor prevAimTarget;
	double healthBarAlpha;

	//===========================================================================
	//
	//
	//
	//===========================================================================

	override void Init(void)
	{
		Super.Init();
		SetSize(0, 320, 200);

		mHUDSmallFont = HUDFont.Create(smallfont);
		mHUDConFont = HUDFont.Create(confont);

		mChexIndexFont = HUDFont.Create("ChexIndexFont");

		InitSevHUD();
	}

	override void NewGame(void)
	{
		Super.NewGame();
		InitSevHUD();
	}

	override void Draw(int state, double TicFrac)
	{
		if ((state == HUD_StatusBar)||(state == HUD_Fullscreen))
		{
			// draw crosshair etc
			BaseStatusBar.Draw(state, TicFrac);

			DrawSevHUD(state);
			DrawHealthBar(state);

			// We basically do fullscreenoffsets = true by calling BeginHUD
			BeginHUD();
			DrawSevAutomapHUD();
		}
	}

	override void DrawAutomapHUD(double ticFrac)
	{
		// do nothing
	}

	//===========================================================================
	//
	//
	//
	//===========================================================================

	void InitSevHUD(void)
	{
		prevAimTarget = NULL;
		healthBarAlpha = 0.;
	}

	//===========================================================================
	//
	//
	//
	//===========================================================================

	void DrawStatusBarStats(int state, string basefilepath, HUDFont fnt)
	{
		int basex = 326;

		// Draw the background first
		// Is there a full stats gfx? If so, use that instead of drawing the individual parts
		int lump;

		DrawImage("graphics/SevHUD/chex/st_kill.png", (basex, 173), DI_ITEM_OFFSETS);
		DrawImage("graphics/SevHUD/chex/st_item.png", (basex, 179), DI_ITEM_OFFSETS);
		DrawImage("graphics/SevHUD/chex/st_secret.png", (basex, 185), DI_ITEM_OFFSETS);
		DrawImage("graphics/SevHUD/chex/st_time.png", (basex, 191), DI_ITEM_OFFSETS);

		int amt;
		int pcnt;
		
		int offsetx = 32;

		// kills
		pcnt = Level.total_monsters ? 100 * Level.killed_monsters / Level.total_monsters : 100;
		amt = multiplayer ? CPlayer.killcount : pcnt;
		DrawString(fnt, FormatNumber(amt, 3), (basex + offsetx, 173), DI_TEXT_ALIGN_RIGHT);

		// items
		pcnt = Level.total_items ? 100 * Level.found_items / Level.total_items : 100;
		amt = multiplayer ? CPlayer.itemcount : pcnt;
		DrawString(fnt, FormatNumber(amt, 3), (basex + offsetx, 179), DI_TEXT_ALIGN_RIGHT);

		// secrets
		pcnt = Level.total_secrets ? 100 * Level.found_secrets / Level.total_secrets : 100;
		amt = multiplayer ? CPlayer.secretcount : pcnt;
		DrawString(fnt, FormatNumber(amt, 3), (basex + offsetx, 185), DI_TEXT_ALIGN_RIGHT);

		// time stuff
		bool sucks = false;
		int hoursoffset = 0;
		int minsdigits = 1;
		int timeSeconds = Thinker.Tics2Seconds(Level.time);
		int hours   = timeSeconds / 3600;
		int minutes = (timeSeconds % 3600) / 60;
		int seconds = timeSeconds % 60;

		// if ((hours >= Level.sucktime) || (hours >= 10))
		if (hours >= 10)
			sucks = true;

		int debugtime = CVar.GetCVar("sevhud_debugtime", CPlayer).GetInt();
		if (debugtime >= 1)
		{
			hours = debugtime;
			minutes = debugtime;
		}

		if (!sucks)
		{
			// hours
			if (hours >= 1)
			{
				hoursoffset = 1;
				amt = hours;
				DrawString(fnt, FormatNumber(amt, 1, format: 2), (basex + 20, 191), DI_TEXT_ALIGN_RIGHT);
				DrawImage("graphics/SevHUD/chex/st_colon.png", (basex + 21, 192), DI_ITEM_OFFSETS);
			}

			// minutes
			DrawImage("graphics/SevHUD/chex/st_colon.png", (basex + 32, 192), DI_ITEM_OFFSETS);
			if ((minutes >= 10) || (hours >= 1))
				minsdigits = 2;
			amt = minutes;
			DrawString(fnt, FormatNumber(amt, minsdigits, format: 2), (basex + 30 + hoursoffset, 191), DI_TEXT_ALIGN_RIGHT);

			// seconds
			amt = seconds;
			DrawString(fnt, FormatNumber(amt, 2, format: 2), (basex + 42, 191), DI_TEXT_ALIGN_RIGHT);
		}
		else
		{
			// time sucks
			DrawImage("graphics/SevHUD/extra/st_sucks.png", (basex + 18, 191), DI_ITEM_OFFSETS);
		}
	}

	//===========================================================================
	//
	//
	//
	//===========================================================================

	void DrawStatusBarPowerups(void)
	{
		// Draw the background first
		DrawImage("graphics/SevHUD/chex/st_extra.png", (-53, 168), DI_ITEM_OFFSETS);

		let lightamp = Powerup(CPlayer.mo.FindInventory("PowerLightAmp"));
		if (lightamp)
			DrawBar("graphics/SevHUD/chex/st_lightamp.png", "graphics/SevHUD/chex/st_lightampback.png", int(Ceil(double(lightamp.EffectTics))), int(Ceil(double(lightamp.MaxEffectTics))), (-21, 172), 0, SHADER_VERT|SHADER_REVERSE, DI_ITEM_OFFSETS, 1.0);

		let radsuit = Powerup(CPlayer.mo.FindInventory("PowerIronFeet"));
		if (radsuit)
			DrawBar("graphics/SevHUD/chex/st_radsuit.png", "graphics/SevHUD/chex/st_radsuitback.png", int(Ceil(double(radSuit.EffectTics))), int(Ceil(double(radSuit.MaxEffectTics))), (-48, 172), 0, SHADER_VERT|SHADER_REVERSE, DI_ITEM_OFFSETS, 1.0);

		let allmap = Level.allmap;
		if (allmap)
			DrawImage("graphics/SevHUD/chex/st_allmap.png", (-21, 181), DI_ITEM_OFFSETS);
	}

	//===========================================================================
	//
	//
	//
	//===========================================================================

	// taken from GZDoom's vdraw.cpp
	int GetUIScale(int altval)
	{
		int scaleval;
		if (altval > 0)
			scaleval = altval;
		else if (uiscale == 0)
		{
			// Default should try to scale to 640x400
			int vscale = Screen.GetHeight() / 400;
			int hscale = Screen.GetWidth() / 640;
			scaleval = clamp(vscale, 1, hscale);
		}
		else scaleval = uiscale;

		// block scales that result in something larger than the current screen.
		int vmax = Screen.GetHeight() / 200;
		int hmax = Screen.GetWidth() / 320;
		int smax = max(vmax, hmax);
		return max(1, min(scaleval, smax));
	}

	//===========================================================================
	//
	//
	//
	//===========================================================================

	void DrawHealthBar(int state)
	{
		if (!CPlayer || !CPlayer.mo)
			return;

		if (!CVar.GetCVar("sevhud_hpbar", CPlayer).GetBool())
			return;

		if (automapactive)
			return;

		let tracer = SevHUDHealthTracer(CPlayer.mo.FindInventory("SevHUDHealthTracer"));
		if (!tracer)
			return;

		if (tracer.currentAimTarget && CPlayer.playerstate == PST_LIVE)
		{
			prevAimTarget = tracer.currentAimTarget;
			healthBarAlpha = 1.0;
		}
		else
		{
			healthBarAlpha *= 0.926;
		}

		if (prevAimTarget)
		{
			// get fonts
			Font fnt = mHUDSmallFont.mFont;

			// fonts are invalid, cancel drawing
			if (!fnt)
				return;

			// retrieve target health
			int health = prevAimTarget.Health;
			int maxHealth = prevAimTarget.GetMaxHealth(true);

			// no need to draw 0 or negative health
			if (health <= 0)
				return;

			// base Y position of the entire HP bar interface
			let scale = GetUIScale(con_scaletext);
			double baseYPos = (fnt.GetHeight() * scale) * con_notifylines;
			baseYPos /= CleanYFac;
			baseYPos += fnt.GetHeight();

			// draw HP bar background and border
			let brdTex = TexMan.CheckForTexture("graphics/SevHUD/HPBarBorder.png");
			if (brdTex.IsValid())
			{
				Vector2 brdTexSize = TexMan.GetScaledSize(brdTex);
				Screen.DrawTexture(brdTex, false,
					(Screen.GetWidth() / 2) - ((brdTexSize.X / 2) * CleanXFac),
					(baseYPos * CleanYFac),
					DTA_CleanNoMove, true,
					DTA_KeepRatio, true,
					DTA_Alpha, healthBarAlpha);
			}

			let bgTex = TexMan.CheckForTexture("graphics/SevHUD/HPBarShade.png");
			if (bgTex.IsValid())
			{
				Vector2 bgTexSize = TexMan.GetScaledSize(bgTex);
				Screen.DrawTexture(bgTex, false,
					(Screen.GetWidth() / 2) - ((bgTexSize.X / 2) * CleanXFac),
					(baseYPos * CleanYFac),
					DTA_CleanNoMove, true,
					DTA_KeepRatio, true,
					DTA_Alpha, 0.66 * healthBarAlpha);
			}

			String txt;

			// draw health bar
			let barTex = TexMan.CheckForTexture("graphics/SevHUD/HPBarHealth.png");
			if (barTex.IsValid())
			{
				Vector2 barTexSize = TexMan.GetScaledSize(barTex);

				// get the starting X pos to clip to
				int clipX = (Screen.GetWidth() / 2) - ((int(barTexSize.X) / 2) * CleanXFac);

				// get the full X size of the texture
				int fullClip = (int(barTexSize.X) * CleanXFac);

				// get health percentage
				int healthPercent = 100 * health / maxHealth;
				int maxClip = fullClip * healthPercent / 100;

				// set the clipping
				clipX += maxClip;

				Screen.DrawTexture(barTex, false,
					(Screen.GetWidth() / 2) - ((barTexSize.X / 2) * CleanXFac),
					(baseYPos * CleanYFac),
					DTA_ClipRight, int(clipX),
					DTA_CleanNoMove, true,
					DTA_KeepRatio, true,
					DTA_Alpha, healthBarAlpha);
			}

			// get font colours
			int normalTxtColor = Font.CR_GREEN;
			int nameTxtColor = Font.CR_WHITE;

			// draw target name
			String targName = prevAimTarget.GetCharacterName();
			if (prevAimTarget is "PlayerPawn" && prevAimTarget.player && playeringame[prevAimTarget.PlayerNumber()])
				targName = prevAimTarget.player.GetUserName();
			txt = String.Format("%s", targName);
			
			// Clean up display names
			if (txt == "FlemoidusCommonusV3")
				txt = "Flemoid";

			if (txt == "FlemoidusBipedicusV3")
				txt = "Bipedicus";

			if (txt == "FlemoidusCycloptisCommonusV3")
				txt = "Cycloptis";

			if (txt == "ArmoredFlemoidusBipedicusV3")
				txt = "Armored Bipedicus";

			if (txt == "FlembraneV3")
				txt = "Flembrane";

			if (txt == "FlemoidusMaximus")
				txt = "Maximus";

			if (txt == "SuperCycloptis")
				txt = "Super Cycloptis";

			if (txt == "FlemoidusStridicus")
				txt = "Stridicus";

			if (txt == "FlemMine")
				txt = "Flem Mine";

			if (txt == "Snotfolus")
				txt = "Lord Snotfolus";
			// The rest of them look okay to me

			Screen.DrawText(fnt, nameTxtColor,
				(Screen.GetWidth() / 2) - ((fnt.StringWidth(txt) / 2) * CleanXFac),
				(baseYPos * CleanYFac) - (fnt.GetHeight() * CleanYFac),
				txt,
				DTA_CleanNoMove, true,
				DTA_KeepRatio, true,
				DTA_Alpha, healthBarAlpha);

			// draw target health in numbers
			txt = String.Format("%d/%d", health, maxHealth);
			Screen.DrawText(fnt, normalTxtColor,
				(Screen.GetWidth() / 2) - ((fnt.StringWidth(txt) / 2) * CleanXFac),
				(baseYPos * CleanYFac) + (fnt.GetHeight() * CleanYFac) - (fnt.GetHeight() * CleanYFac) + (3 * CleanYFac),
				txt,
				DTA_CleanNoMove, true,
				DTA_KeepRatio, true,
				DTA_Alpha, healthBarAlpha);
		}
	}

	//===========================================================================
	//
	//
	//
	//===========================================================================

	void DrawSevHUDAmmo(HUDFont fnt)
	{
		int amt1, maxamt;
		[amt1, maxamt] = GetAmount("Clip");
		DrawString(fnt, FormatNumber(amt1, 3), (287, 173), DI_TEXT_ALIGN_RIGHT);
		DrawString(fnt, FormatNumber(maxamt, 3), (313, 173), DI_TEXT_ALIGN_RIGHT);

		[amt1, maxamt] = GetAmount("Shell");
		DrawString(fnt, FormatNumber(amt1, 3), (287, 179), DI_TEXT_ALIGN_RIGHT);
		DrawString(fnt, FormatNumber(maxamt, 3), (313, 179), DI_TEXT_ALIGN_RIGHT);

		[amt1, maxamt] = GetAmount("RocketAmmo");
		DrawString(fnt, FormatNumber(amt1, 3), (287, 185), DI_TEXT_ALIGN_RIGHT);
		DrawString(fnt, FormatNumber(maxamt, 3), (313, 185), DI_TEXT_ALIGN_RIGHT);

		[amt1, maxamt] = GetAmount("Cell");
		DrawString(fnt, FormatNumber(amt1, 3), (287, 191), DI_TEXT_ALIGN_RIGHT);
		DrawString(fnt, FormatNumber(maxamt, 3), (313, 191), DI_TEXT_ALIGN_RIGHT);
	}

	//===========================================================================
	//
	//
	//
	//===========================================================================

	void DrawSevAutomapHUD(void)
	{
		if (!automapactive)
		return;
	
		HUDFont fnt = mChexIndexFont;
		int fontHeight = fnt.mFont.GetHeight();
		
		int lump;
		int amt;

		int basey = 4;
		int offsety = 0;

		// Global timer
		if (CVar.GetCVar("am_showtotaltime", CPlayer).GetBool())
		{
			int hoursoffset = 0;
			int hoursdigits = 1;
			int minsdigits = 1;
			int timeSeconds = Thinker.Tics2Seconds(Level.totaltime);
			int hours   = timeSeconds / 3600;
			int minutes = (timeSeconds % 3600) / 60;
			int seconds = timeSeconds % 60;

			int debugtime = CVar.GetCVar("sevhud_debugtime", CPlayer).GetInt();
			if (debugtime >= 1)
			{
				hours = debugtime;
				minutes = debugtime;
			}

			// hours
			if (hours >= 1)
			{
				if (hours >= 10)
					hoursdigits = 2;

				if (hours >= 100)
					hoursdigits = 3;

				hoursoffset = 1;
				amt = hours;
				DrawString(fnt, FormatNumber(amt, hoursdigits, format: 2), (-29, basey), DI_TEXT_ALIGN_RIGHT);
				DrawImage("graphics/SevHUD/chex/st_colon.png", (-28, basey + 1), DI_ITEM_OFFSETS);
			}

			// minutes
			if ((minutes >= 10) || (hours >= 1))
				minsdigits = 2;
			amt = minutes;
			DrawString(fnt, FormatNumber(amt, minsdigits, format: 2), (hoursoffset-19, basey), DI_TEXT_ALIGN_RIGHT);

			DrawImage("graphics/SevHUD/chex/st_colon.png", (-17, basey + 1), DI_ITEM_OFFSETS);

			// seconds
			amt = seconds;
			DrawString(fnt, FormatNumber(amt, 2, format: 2), (-7, basey), DI_TEXT_ALIGN_RIGHT);
			
			// move the level timer down
			offsety += fontHeight;
		}

		// Don't draw anything else if we're already drawing the stats on the status bar
		if (CVar.GetCVar("sevhud_extra", CPlayer).GetInt())
			return;
		
		// Map time
		if (CVar.GetCVar("am_showtime", CPlayer).GetBool())
		{
			int hoursoffset = 0;
			int hoursdigits = 1;
			int minsdigits = 1;
			int timeSeconds = Thinker.Tics2Seconds(Level.time);
			int hours   = timeSeconds / 3600;
			int minutes = (timeSeconds % 3600) / 60;
			int seconds = timeSeconds % 60;

			int debugtime = CVar.GetCVar("sevhud_debugtime", CPlayer).GetInt();
			if (debugtime >= 1)
			{
				hours = debugtime;
				minutes = debugtime;
			}

			// hours
			if (hours >= 1)
			{
				if (hours >= 10)
					hoursdigits = 2;

				if (hours >= 100)
					hoursdigits = 3;

				hoursoffset = 1;
				amt = hours;
				DrawString(fnt, FormatNumber(amt, hoursdigits, format: 2), (-29, basey + offsety), DI_TEXT_ALIGN_RIGHT);
				DrawImage("graphics/SevHUD/chex/st_colon.png", (-28, basey + offsety + 1), DI_ITEM_OFFSETS);
			}

			// minutes
			if ((minutes >= 10) || (hours >= 1))
				minsdigits = 2;
			amt = minutes;
			DrawString(fnt, FormatNumber(amt, minsdigits, format: 2), (hoursoffset-19, basey + offsety), DI_TEXT_ALIGN_RIGHT);

			DrawImage("graphics/SevHUD/chex/st_colon.png", (-17, basey + offsety + 1), DI_ITEM_OFFSETS);

			// seconds
			amt = seconds;
			DrawString(fnt, FormatNumber(amt, 2, format: 2), (-7, basey + offsety), DI_TEXT_ALIGN_RIGHT);
		}

		int pcnt;
		basey = -33;
		offsety = 0;

		if (CVar.GetCVar("am_showsecrets", CPlayer).GetBool())
		{
			// Draw automap secrets
			DrawImage("graphics/SevHUD/chex/st_secret.png", (-48, basey-fontHeight), DI_ITEM_OFFSETS);
			pcnt = Level.total_secrets ? 100 * Level.found_secrets / Level.total_secrets : 100;
			amt = multiplayer ? CPlayer.secretcount : pcnt;
			DrawString(fnt, FormatNumber(amt, 3), (-16, basey-fontHeight), DI_TEXT_ALIGN_RIGHT);

			offsety -= fontHeight;
		}

		if (CVar.GetCVar("am_showitems", CPlayer).GetBool())
		{
			// Draw automap items
			DrawImage("graphics/SevHUD/chex/st_item.png", (-48, offsety+basey-fontHeight), DI_ITEM_OFFSETS);
			pcnt = Level.total_items ? 100 * Level.found_items / Level.total_items : 100;
			amt = multiplayer ? CPlayer.itemcount : pcnt;
			DrawString(fnt, FormatNumber(amt, 3), (-16, offsety+basey-fontHeight), DI_TEXT_ALIGN_RIGHT);

			offsety -= fontHeight;
		}

		if (CVar.GetCVar("am_showmonsters", CPlayer).GetBool())
		{
			// Draw automap kills
			DrawImage("graphics/SevHUD/chex/st_kill.png", (-48, offsety+basey-fontHeight), DI_ITEM_OFFSETS);
			pcnt = Level.total_monsters ? 100 * Level.killed_monsters / Level.total_monsters : 100;
			amt = multiplayer ? CPlayer.killcount : pcnt;
			DrawString(fnt, FormatNumber(amt, 3), (-16, offsety+basey-fontHeight), DI_TEXT_ALIGN_RIGHT);
		}
	}

	//===========================================================================
	//
	//
	//
	//===========================================================================

	void DrawSevHUD(int state)
	{
		HUDFont fnt = mChexIndexFont;

		int amt;

		int basey = 4;
		int offsety = 0;

		string filepath = "graphics/SevHUD/chex/";

		if (CVar.GetCVar("sevhud_extra", CPlayer).GetBool())
		{
			DrawStatusBarStats(state, filepath, fnt);
			DrawStatusBarPowerups();
		}

		// DrawMainBar
		if (automapactive)
		{
			// Map label/name
			HUDFont fnt = mHUDSmallFont;
			int fontHeight = fnt.mFont.GetHeight();

			string maplabel = "";
			string mapname = "";

			int am_showmaplabel = CVar.GetCVar("am_showmaplabel", CPlayer).GetInt();
			bool showmaplabel = false;
			bool showmapname = CVar.GetCVar("am_showlevelname", CPlayer).GetBool();

			if ((am_showmaplabel == 1) || (am_showmaplabel == 2) && (!(Level.clusterflags & Level.CLUSTER_HUB)))
				showmaplabel = true;

			if (showmaplabel)
			{
				maplabel = Level.MapName;
				if (showmapname)
					maplabel = maplabel..": ";
			}

			if (showmapname)
			{
				mapname = (Level.Levelname);
			}

			DrawString(fnt, maplabel.."\cj"..mapname, (160, 167-fontHeight), DI_TEXT_ALIGN_CENTER);
		}

		if (CVar.GetCVar("sevhud_chexextra", CPlayer).GetBool())
		{
			DrawImage(filepath.."st_bar.png", (0, 168), DI_ITEM_OFFSETS);
		}
		else
		{
			DrawImage(filepath.."st_barchex.png", (0, 168), DI_ITEM_OFFSETS);
		}
		DrawImage("STTPRCNT", (90, 171), DI_ITEM_OFFSETS);
		DrawImage("STTPRCNT", (221, 171), DI_ITEM_OFFSETS);

		Inventory a1 = GetCurrentAmmo();
		if (a1 != null) DrawString(mHUDFont, FormatNumber(a1.Amount, 3), (44, 171), DI_TEXT_ALIGN_RIGHT|DI_NOSHADOW);
		DrawString(mHUDFont, FormatNumber(CPlayer.health, 3), (90, 171), DI_TEXT_ALIGN_RIGHT|DI_NOSHADOW);
		DrawString(mHUDFont, FormatNumber(GetArmorAmount(), 3), (221, 171), DI_TEXT_ALIGN_RIGHT|DI_NOSHADOW);

		DoomStatusBar.DrawBarKeys();

		if (deathmatch || teamplay)
		{
			DrawString(mHUDFont, FormatNumber(CPlayer.FragCount, 3), (138, 171), DI_TEXT_ALIGN_RIGHT);
		}

		if (multiplayer)
		{
			DrawImage("STFBANY", (143, 168), DI_ITEM_OFFSETS|DI_TRANSLATABLE);
		}

		if (CPlayer.mo.InvSel != null && !Level.NoInventoryBar)
		{
			DrawInventoryIcon(CPlayer.mo.InvSel, (160, 198), DI_DIMDEPLETED);
			if (CPlayer.mo.InvSel.Amount > 1)
			{
				DrawString(mAmountFont, FormatNumber(CPlayer.mo.InvSel.Amount), (175, 198-mIndexFont.mFont.GetHeight()), DI_TEXT_ALIGN_RIGHT, Font.CR_GOLD);
			}
		}
		else
		{
			DrawTexture(GetMugShot(5), (143, 168), DI_ITEM_OFFSETS);
		}
		if (isInventoryBarVisible())
		{
			DrawInventoryBar(diparms, (48, 169), 7, DI_ITEM_LEFT_TOP);
		}

		if (CVar.GetCVar("sevhud_chexextra", CPlayer).GetBool())
		{
			DrawSevHUDAmmo(fnt);
			// Draw "ARMS" text
			DrawImage(filepath.."st_arms.png", (104, 168), DI_ITEM_OFFSETS);
			// Draw weapons numbers
			DrawImage(CPlayer.HasWeaponsInSlot(2)? filepath.."st_ynum2.png" : filepath.."st_gnum2.png", (111, 172), DI_ITEM_OFFSETS);
			DrawImage(CPlayer.HasWeaponsInSlot(3)? filepath.."st_ynum3.png" : filepath.."st_gnum3.png", (123, 172), DI_ITEM_OFFSETS);
			DrawImage(CPlayer.HasWeaponsInSlot(4)? filepath.."st_ynum4.png" : filepath.."st_gnum4.png", (135, 172), DI_ITEM_OFFSETS);
			DrawImage(CPlayer.HasWeaponsInSlot(5)? filepath.."st_ynum5.png" : filepath.."st_gnum5.png", (111, 182), DI_ITEM_OFFSETS);
			DrawImage(CPlayer.HasWeaponsInSlot(6)? filepath.."st_ynum6.png" : filepath.."st_gnum6.png", (123, 182), DI_ITEM_OFFSETS);
			DrawImage(CPlayer.HasWeaponsInSlot(7)? filepath.."st_ynum7.png" : filepath.."st_gnum7.png", (135, 182), DI_ITEM_OFFSETS);
		}
	}
}
