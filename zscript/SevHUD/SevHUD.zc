//===========================================================================
//
// SevHUD
//
// ZScript code: Copyright (C) 2020 - 2025 Nash Muhandes
// Modifications: Copyright (C) 2025 SeventhSentinel
//
// License: MIT
//
//===========================================================================

class SevHUD : DoomStatusBar
{
	HUDFont mHUDSmallFont;
	HUDFont mHUDConFont;

	HUDFont mDefaultIndexFont;
	HUDFont mBTSX1IndexFont;
	HUDFont mBTSX2IndexFont;

	Actor prevAimTarget;
	double healthBarAlpha;
	
	bool gotchecksum;
	string checksum;
	bool gotmapset;
	string mapset;
	HUDFont littleNumbers;
	string powerups;

	vector2 invuloffset;
	vector2 lightampoffset;
	vector2 invisoffset;
	vector2 radsuitoffset;

	// To be used for Sentinel's Lexicon support later
	// name curmap;
	// name prevmap;

	//===========================================================================
	//
	//
	//
	//===========================================================================

	override void Init(void)
	{
		Super.Init();
		SetSize(0, 320, 200);

		mHUDSmallFont = HUDFont.Create(smallfont);
		mHUDConFont = HUDFont.Create(confont);

		mDefaultIndexFont = HUDFont.Create("DefaultIndexFont");
		mBTSX1IndexFont = HUDFont.Create("BTSX1IndexFont");
		mBTSX2IndexFont = HUDFont.Create("BTSX2IndexFont");

		InitSevHUD();
	}

	override void NewGame(void)
	{
		Super.NewGame();
		InitSevHUD();
	}

	override void Draw(int state, double TicFrac)
	{
		if ((state == HUD_StatusBar)||(state == HUD_Fullscreen))
		{
			// draw crosshair etc
			BaseStatusBar.Draw(state, TicFrac);

			DrawSevHUD(state);
			DrawHealthBar(state);

			// We basically do fullscreenoffsets = true by calling BeginHUD
			BeginHUD();
			DrawSevAutomapHUD();
		}
	}

	override void DrawAutomapHUD(double ticFrac)
	{
		// do nothing
	}

	//===========================================================================
	//
	//
	//
	//===========================================================================

	void InitSevHUD(void)
	{
		prevAimTarget = NULL;
		healthBarAlpha = 0.;
		gotchecksum = false;
	}

	//===========================================================================
	//
	//
	//
	//===========================================================================

	void PrintChecksum(void)
	{
		checksum = Level.GetChecksum();
		Console.Printf("Current map checksum: %s", checksum);
		gotchecksum = true;
	}

	//===========================================================================
	//
	//
	//
	//===========================================================================

	void GetMapSet(void)
	{
		// This feels hacky! I don't like it!
		// I'd like it to be able to run more than once for the sake of Sentinel's Lexicon. I'll worry about that later.
		if (!gotmapset)
		{
			// Set defaults
			mapset = "standard";
			powerups = "standard";
			littleNumbers = mDefaultIndexFont;

			// Graphic offsets don't work for drawing bars, so we'll add them here instead
			invuloffset = (0, 0);
			lightampoffset = (0, 0);
			invisoffset = (0, 0);
			radsuitoffset = (0, 0);

			// Console.Printf("Running!");

			// Any mapsets commented out have license text or similar stating that reuse of their assets is not permitted,
			// but I've left the checksums in if you'd like to make your own for private use.

			// Grab a map checksum to figure out what mapset we're in
			/* if (LevelInfo.MapExists("E1M1"))
			{
				name checksum = LevelInfo.MapChecksum("E1M1");

				switch (checksum)
				{
					// TODO: add some Doom 1 wads
				}
			} */
			
			if (LevelInfo.MapExists("E2M1"))
			{
				name checksum = LevelInfo.MapChecksum("E2M1");

				switch (checksum)
				{
					// Aliens TC
					// First version
					case '1c95f245b71e5daf4824d4c715abcf7c':
					// Second version
					case 'ec2fb7f588a298d232923aa3b986ffd2':
					// Some version of it I found on Reddit that claims to make it work better in GZDoom
					// I don't think it does, but I already got the checksum, so here ya go
					case 'f27e523702be6c2d14b2e83983af218a':
						mapset = "alienstc";
						break;
				}
			}

			if (LevelInfo.MapExists("MAP01"))
			{
				name checksum = LevelInfo.MapChecksum("MAP01");

				switch (checksum)
				{
					// Aliens TC Doom 2 version
					case '6ef79e89e562aab57199af1d572033f4':
						mapset = "alienstc";
						break;

					// Ancient Aliens
					// This one has a pretty normal status bar, but it replaces the powerup gfx
					// So we'll make a folder just for the doom ones and set it
					// That way it looks correct in Freedoom
					case 'acac693dd139d067a57b17620e624c31':
						powerups = "doom";
						break;

					// BTSX ep 1
					 case 'ff9c6545037c5e5e27ce0dacd24cdd37':
						mapset = "btsx1";
						powerups = "doom";
						littleNumbers = mBTSX1IndexFont;
						break;
					
					// BTSX ep 2
					case '067cf96073db3cc4a8d887ee240f88da':
						mapset = "btsx2";
						powerups = "doom";
						littleNumbers = mBTSX2IndexFont;
						break; 

					// Eviternity
					case '9831c412420f16fa0de000fd1dbfe901':
						mapset = "eviternity";
						break;

					// Eviternity 2
					case '8eb38d5289c47bb68d64f2832efa096d':
						mapset = "eviternity2";
						break;

					// Legacy of Rust
					// Despite the map label saying it's E1M1, it is in fact MAP01
					case 'bf34c34c5dfc8bb47228cc304f9a6748':
						mapset = "legacyofrust";
						powerups = mapset;
						break;

					// Pirate Doom 2
					case '8af76bf89a864c1e2b3b21865c277298':
						mapset = "piratedoom2";
						powerups = mapset;
						radsuitoffset = (-15, 1);
						break;

					// Plutonia 2
					case '26901fc48667b76027871880e357cd0a':
					// PRCP
					case '4803362bd33b5e11956f6682dfd48900':
						mapset = "pl2";
						break; 
				}
			}

			if (LevelInfo.MapExists("MAP41"))
			{
				name checksum = LevelInfo.MapChecksum("MAP41");

				switch (checksum)
				{
					// Pirate Doom
					case 'baebc37899109027a7e26f2e5390b0b1':
						mapset = "piratedoom";
						powerups = mapset;
						invuloffset = (6, 0);
						invisoffset = (-6, 0);
						break;
				}
			}

			/*
				if (LevelInfo.MapExists("VR"))
			{
				name vrchecksum = LevelInfo.MapChecksum("VR");
				if (vrchecksum = '47b55a4612b6c2891607c52a39ea62ef')
				{
					// Sentinel's Lexicon support
					// Console.Printf("You're playing The Sentinel's Lexicon!");
					if (curmap)
						prevmap = curmap;

					curmap = level.Mapname;

					switch (curmap)
					{
						case 'vr':
							mapset = "none";
							break;
					}
				}
			}
			*/
		gotmapset = true;
		}
	}

	//===========================================================================
	//
	//
	//
	//===========================================================================

	void DrawStatusBarStats(int state, string basefilepath, HUDFont fnt)
	{
		int basex = 326;

		// Draw the background first
		// Is there a full stats gfx? If so, use that instead of drawing the individual parts
		int lump;

		lump = Wads.CheckNumForFullName(basefilepath..mapset.."/st_stats.png");
		if (lump >= 0)
		{
			DrawImage(basefilepath..mapset.."/st_stats.png", (320, 168), DI_ITEM_OFFSETS);
		}
		else
		{
			lump = Wads.CheckNumForFullName(basefilepath..mapset.."/st_kill.png");
			if (lump >= 0)
			{
				DrawImage(basefilepath..mapset.."/st_kill.png", (basex, 173), DI_ITEM_OFFSETS);
				DrawImage(basefilepath..mapset.."/st_item.png", (basex, 179), DI_ITEM_OFFSETS);
				DrawImage(basefilepath..mapset.."/st_secret.png", (basex, 185), DI_ITEM_OFFSETS);
				DrawImage(basefilepath..mapset.."/st_time.png", (basex, 191), DI_ITEM_OFFSETS);
			}
			else
			{
				DrawImage(basefilepath.."standard/st_kill.png", (basex, 173), DI_ITEM_OFFSETS);
				DrawImage(basefilepath.."standard/st_item.png", (basex, 179), DI_ITEM_OFFSETS);
				DrawImage(basefilepath.."standard/st_secret.png", (basex, 185), DI_ITEM_OFFSETS);
				DrawImage(basefilepath.."standard/st_time.png", (basex, 191), DI_ITEM_OFFSETS);
			}
		}
		
		string imgcolon;
		lump = Wads.CheckNumForFullName(basefilepath..mapset.."/st_colon.png");
		if (lump >= 0)
			imgcolon = basefilepath..mapset.."/st_colon.png";
		else
			imgcolon = basefilepath.."standard/st_colon.png";

		int amt;
		int pcnt;
		
		int offsetx = 32;

		// kills
		pcnt = Level.total_monsters ? 100 * Level.killed_monsters / Level.total_monsters : 100;
		amt = multiplayer ? CPlayer.killcount : pcnt;
		DrawString(fnt, FormatNumber(amt, 3), (basex + offsetx, 173), DI_TEXT_ALIGN_RIGHT);

		// items
		pcnt = Level.total_items ? 100 * Level.found_items / Level.total_items : 100;
		amt = multiplayer ? CPlayer.itemcount : pcnt;
		DrawString(fnt, FormatNumber(amt, 3), (basex + offsetx, 179), DI_TEXT_ALIGN_RIGHT);

		// secrets
		pcnt = Level.total_secrets ? 100 * Level.found_secrets / Level.total_secrets : 100;
		amt = multiplayer ? CPlayer.secretcount : pcnt;
		DrawString(fnt, FormatNumber(amt, 3), (basex + offsetx, 185), DI_TEXT_ALIGN_RIGHT);

		// time stuff
		bool sucks = false;
		int hoursoffset = 0;
		int minsdigits = 1;
		int timeSeconds = Thinker.Tics2Seconds(Level.time);
		int hours   = timeSeconds / 3600;
		int minutes = (timeSeconds % 3600) / 60;
		int seconds = timeSeconds % 60;

		// if ((hours >= Level.sucktime) || (hours >= 10))
		if (hours >= 10)
			sucks = true;

		int debugtime = CVar.GetCVar("sevhud_debugtime", CPlayer).GetInt();
		if (debugtime >= 1)
		{
			hours = debugtime;
			minutes = debugtime;
		}

		if (!sucks)
		{
			// hours
			if (hours >= 1)
			{
				hoursoffset = 1;
				amt = hours;
				DrawString(fnt, FormatNumber(amt, 1, format: 2), (basex + 20, 191), DI_TEXT_ALIGN_RIGHT);
				DrawImage(imgcolon, (basex + 21, 192), DI_ITEM_OFFSETS);
			}

			// minutes
			DrawImage(imgcolon, (basex + 32, 192), DI_ITEM_OFFSETS);
			if ((minutes >= 10) || (hours >= 1))
				minsdigits = 2;
			amt = minutes;
			DrawString(fnt, FormatNumber(amt, minsdigits, format: 2), (basex + 30 + hoursoffset, 191), DI_TEXT_ALIGN_RIGHT);

			// seconds
			amt = seconds;
			DrawString(fnt, FormatNumber(amt, 2, format: 2), (basex + 42, 191), DI_TEXT_ALIGN_RIGHT);
		}
		else
		{
			// time sucks
			DrawImage("graphics/SevHUD/extra/st_sucks.png", (basex + 18, 191), DI_ITEM_OFFSETS);
		}
	}

	//===========================================================================
	//
	//
	//
	//===========================================================================

	void DrawStatusBarPowerups(string basefilepath)
	{
	
		// Get powerup states
		let invul = Powerup(CPlayer.mo.FindInventory("PowerInvulnerable"));
		let lightamp = Powerup(CPlayer.mo.FindInventory("PowerLightAmp"));
		let invis = Powerup(CPlayer.mo.FindInventory("PowerInvisibility"));
		let berserk = CPlayer.mo.FindInventory("PowerStrength");
		let radsuit = Powerup(CPlayer.mo.FindInventory("PowerIronFeet"));
		let allmap = Level.allmap;
		
		if (powerups == "standard")
		{
			// Draw standard gfx
			DrawImage(basefilepath.."extra/st_extra.png", (-53, 168), DI_ITEM_OFFSETS);

			if (invul)
				DrawBar(basefilepath.."extra/st_invul.png", basefilepath.."extra_back/st_orbback.png", int(Ceil(double(invul.EffectTics))), int(Ceil(double(invul.MaxEffectTics))), (-48, 172), 0, SHADER_VERT|SHADER_REVERSE, DI_ITEM_OFFSETS, 1.0);
				// These specialized blank bar background graphics help the foreground graphic draw better. I was getting erroneous black pixels otherwise

			if (lightamp)
				DrawBar(basefilepath.."extra/st_lightamp.png", basefilepath.."extra_back/st_lightampback.png", int(Ceil(double(lightamp.EffectTics))), int(Ceil(double(lightamp.MaxEffectTics))), (-33, 174), 0, SHADER_VERT|SHADER_REVERSE, DI_ITEM_OFFSETS, 1.0);

			if (invis)
				DrawBar(basefilepath.."extra/st_invis.png", basefilepath.."extra_back/st_orbback.png", int(Ceil(double(invis.EffectTics))), int(Ceil(double(invis.MaxEffectTics))), (-17, 172), 0, SHADER_VERT|SHADER_REVERSE, DI_ITEM_OFFSETS, 1.0);

			if (berserk)
				DrawImage(basefilepath.."extra/st_berserk.png", (-48, 188), DI_ITEM_OFFSETS);

			if (radsuit)
				DrawBar(basefilepath.."extra/st_radsuit.png", basefilepath.."extra_back/st_radsuitback.png", int(Ceil(double(radSuit.EffectTics))), int(Ceil(double(radSuit.MaxEffectTics))), (-33, 185), 0, SHADER_VERT|SHADER_REVERSE, DI_ITEM_OFFSETS, 1.0);

			if (allmap)
				DrawImage(basefilepath.."extra/st_allmap.png", (-17, 186), DI_ITEM_OFFSETS);
		}
		else
		{
			// Draw mapset-specific gfx
			DrawImage(basefilepath..powerups.."/st_extra.png", (-53, 168), DI_ITEM_OFFSETS);

			if (invul)
				DrawBar(basefilepath..powerups.."/st_invul.png", basefilepath..powerups.."/st_invulback.png", int(Ceil(double(invul.EffectTics))), int(Ceil(double(invul.MaxEffectTics))), invuloffset + (-48, 172), 0, SHADER_VERT|SHADER_REVERSE, DI_ITEM_OFFSETS, 1.0);

			if (lightamp)
				DrawBar(basefilepath..powerups.."/st_lightamp.png", basefilepath..powerups.."/st_lightampback.png", int(Ceil(double(lightamp.EffectTics))), int(Ceil(double(lightamp.MaxEffectTics))), lightampoffset + (-33, 174), 0, SHADER_VERT|SHADER_REVERSE, DI_ITEM_OFFSETS, 1.0);

			if (invis)
				DrawBar(basefilepath..powerups.."/st_invis.png", basefilepath..powerups.."/st_invisback.png", int(Ceil(double(invis.EffectTics))), int(Ceil(double(invis.MaxEffectTics))), invisoffset + (-17, 172), 0, SHADER_VERT|SHADER_REVERSE, DI_ITEM_OFFSETS, 1.0);

			if (berserk)
				DrawImage(basefilepath..powerups.."/st_berserk.png", (-48, 188), DI_ITEM_OFFSETS);

			if (radsuit)
				DrawBar(basefilepath..powerups.."/st_radsuit.png", basefilepath..powerups.."/st_radsuitback.png", int(Ceil(double(radSuit.EffectTics))), int(Ceil(double(radSuit.MaxEffectTics))), radsuitoffset + (-33, 185), 0, SHADER_VERT|SHADER_REVERSE, DI_ITEM_OFFSETS, 1.0);

			if (allmap)
				DrawImage(basefilepath..powerups.."/st_allmap.png", (-17, 186), DI_ITEM_OFFSETS);
		}
	}

	//===========================================================================
	//
	//
	//
	//===========================================================================

	// taken from GZDoom's vdraw.cpp
	int GetUIScale(int altval)
	{
		int scaleval;
		if (altval > 0)
			scaleval = altval;
		else if (uiscale == 0)
		{
			// Default should try to scale to 640x400
			int vscale = Screen.GetHeight() / 400;
			int hscale = Screen.GetWidth() / 640;
			scaleval = clamp(vscale, 1, hscale);
		}
		else scaleval = uiscale;

		// block scales that result in something larger than the current screen.
		int vmax = Screen.GetHeight() / 200;
		int hmax = Screen.GetWidth() / 320;
		int smax = max(vmax, hmax);
		return max(1, min(scaleval, smax));
	}

	//===========================================================================
	//
	//
	//
	//===========================================================================

	void DrawHealthBar(int state)
	{
		if (!CPlayer || !CPlayer.mo)
			return;

		if (!CVar.GetCVar("sevhud_hpbar", CPlayer).GetBool())
			return;

		if (automapactive)
			return;

		let tracer = SevHUDHealthTracer(CPlayer.mo.FindInventory("SevHUDHealthTracer"));
		if (!tracer)
			return;

		if (tracer.currentAimTarget && CPlayer.playerstate == PST_LIVE)
		{
			prevAimTarget = tracer.currentAimTarget;
			healthBarAlpha = 1.0;
		}
		else
		{
			healthBarAlpha *= 0.926;
		}

		if (prevAimTarget)
		{
			// get fonts
			Font fnt = mHUDSmallFont.mFont;

			// fonts are invalid, cancel drawing
			if (!fnt)
				return;

			// retrieve target health
			int health = prevAimTarget.Health;
			int maxHealth = prevAimTarget.GetMaxHealth(true);

			String txt;

			// Get target name
			String targName = prevAimTarget.GetCharacterName();
			if (prevAimTarget is "PlayerPawn" && prevAimTarget.player && playeringame[prevAimTarget.PlayerNumber()])
				targName = prevAimTarget.player.GetUserName();
			txt = String.Format("%s", targName);

			// Pirate Doom's LeChuck comes with his own health display
			if ((mapset == "piratedoom") && ((txt == "lechuckteleport") || (txt == "LeChuck")))
				return;

			// Clean up target name
			if (mapset == "piratedoom")
			{
				if ((txt == "Witch Doctor") && ((maxHealth == 80) || (maxHealth == 50)))
					txt = "Cannibal Pygmy";
			}

			if (mapset == "legacyofrust")
			{
				if (txt == "Deh_Actor_150")
					txt = "Ghoul";

				if (txt == "Deh_Actor_151")
					txt = "Banshee";

				if (txt == "Deh_Actor_152")
					txt = "Mindweaver";

				if (txt == "Deh_Actor_153")
					txt = "Shocktrooper";

				if (txt == "Deh_Actor_154")
					txt = "Vassago";

				if (txt == "Deh_Actor_155")
					txt = "Tyrant";
			}

			if (mapset == "piratedoom2")
			{
				if ((txt == "BOLTSHOOTER ARACHNARCH") && (maxHealth == 30))
					txt = "Mad Tribal Minion";

				if (txt == "Cyberdemon")
					txt = "The Clompin' Cannoneer";

				if (txt == "Spider Mastermind")
					txt = "The Rusty Bootlegs Ma'am";

				if (txt == "BossEye")
					txt = "LeChuck, the Zombie Pirate";
			}

			// no need to draw 0 or negative health
			if (health <= 0)
				return;

			// base Y position of the entire HP bar interface
			let scale = GetUIScale(con_scaletext);
			double baseYPos = (fnt.GetHeight() * scale) * con_notifylines;
			baseYPos /= CleanYFac;
			baseYPos += fnt.GetHeight();

			// draw HP bar background and border
			let brdTex = TexMan.CheckForTexture("graphics/SevHUD/HPBarBorder.png");
			if (brdTex.IsValid())
			{
				Vector2 brdTexSize = TexMan.GetScaledSize(brdTex);
				Screen.DrawTexture(brdTex, false,
					(Screen.GetWidth() / 2) - ((brdTexSize.X / 2) * CleanXFac),
					(baseYPos * CleanYFac),
					DTA_CleanNoMove, true,
					DTA_KeepRatio, true,
					DTA_Alpha, healthBarAlpha);
			}

			let bgTex = TexMan.CheckForTexture("graphics/SevHUD/HPBarShade.png");
			if (bgTex.IsValid())
			{
				Vector2 bgTexSize = TexMan.GetScaledSize(bgTex);
				Screen.DrawTexture(bgTex, false,
					(Screen.GetWidth() / 2) - ((bgTexSize.X / 2) * CleanXFac),
					(baseYPos * CleanYFac),
					DTA_CleanNoMove, true,
					DTA_KeepRatio, true,
					DTA_Alpha, 0.66 * healthBarAlpha);
			}

			// draw health bar
			let barTex = TexMan.CheckForTexture("graphics/SevHUD/HPBarHealth.png");
			if (barTex.IsValid())
			{
				Vector2 barTexSize = TexMan.GetScaledSize(barTex);

				// get the starting X pos to clip to
				int clipX = (Screen.GetWidth() / 2) - ((int(barTexSize.X) / 2) * CleanXFac);

				// get the full X size of the texture
				int fullClip = (int(barTexSize.X) * CleanXFac);

				// get health percentage
				int healthPercent = 100 * health / maxHealth;
				int maxClip = fullClip * healthPercent / 100;

				// set the clipping
				clipX += maxClip;

				Screen.DrawTexture(barTex, false,
					(Screen.GetWidth() / 2) - ((barTexSize.X / 2) * CleanXFac),
					(baseYPos * CleanYFac),
					DTA_ClipRight, int(clipX),
					DTA_CleanNoMove, true,
					DTA_KeepRatio, true,
					DTA_Alpha, healthBarAlpha);
			}

			// get font colours
			int normalTxtColor = Font.CR_RED;
			int nameTxtColor = Font.CR_WHITE;

			// Draw target name
			Screen.DrawText(fnt, nameTxtColor,
				(Screen.GetWidth() / 2) - ((fnt.StringWidth(txt) / 2) * CleanXFac),
				(baseYPos * CleanYFac) - (fnt.GetHeight() * CleanYFac),
				txt,
				DTA_CleanNoMove, true,
				DTA_KeepRatio, true,
				DTA_Alpha, healthBarAlpha);

			// draw target health in numbers
			txt = String.Format("%d/%d", health, maxHealth);
			Screen.DrawText(fnt, normalTxtColor,
				(Screen.GetWidth() / 2) - ((fnt.StringWidth(txt) / 2) * CleanXFac),
				(baseYPos * CleanYFac) + (fnt.GetHeight() * CleanYFac) - (fnt.GetHeight() * CleanYFac) + (3 * CleanYFac),
				txt,
				DTA_CleanNoMove, true,
				DTA_KeepRatio, true,
				DTA_Alpha, healthBarAlpha);
		}
	}

	//===========================================================================
	//
	//
	//
	//===========================================================================

	void DrawSevHUDAmmo(HUDFont fnt)
	{
		string ammo1 = "Clip";
		string ammo2 = "Shell";
		string ammo3 = "RocketAmmo";
		string ammo4 = "Cell";

		// Shiver me timbers! Pirate Doom uses unique ammo!
		if (mapset == "piratedoom")
		{
			ammo1 = "Bullets";
			ammo3 = "BallAmmo";
			ammo4 = "DynamiteAmmo";
		}

		int amt1, maxamt;
		[amt1, maxamt] = GetAmount(ammo1);
		DrawString(fnt, FormatNumber(amt1, 3), (287, 173), DI_TEXT_ALIGN_RIGHT);
		DrawString(fnt, FormatNumber(maxamt, 3), (313, 173), DI_TEXT_ALIGN_RIGHT);

		[amt1, maxamt] = GetAmount(ammo2);
		DrawString(fnt, FormatNumber(amt1, 3), (287, 179), DI_TEXT_ALIGN_RIGHT);
		DrawString(fnt, FormatNumber(maxamt, 3), (313, 179), DI_TEXT_ALIGN_RIGHT);

		[amt1, maxamt] = GetAmount(ammo3);
		DrawString(fnt, FormatNumber(amt1, 3), (287, 185), DI_TEXT_ALIGN_RIGHT);
		DrawString(fnt, FormatNumber(maxamt, 3), (313, 185), DI_TEXT_ALIGN_RIGHT);

		[amt1, maxamt] = GetAmount(ammo4);
		DrawString(fnt, FormatNumber(amt1, 3), (287, 191), DI_TEXT_ALIGN_RIGHT);
		DrawString(fnt, FormatNumber(maxamt, 3), (313, 191), DI_TEXT_ALIGN_RIGHT);
	}

	//===========================================================================
	//
	//
	//
	//===========================================================================

	void DrawSevAutomapHUD(void)
	{
		if (!automapactive)
		return;
	
		HUDFont fnt = littleNumbers;
		int fontHeight = fnt.mFont.GetHeight();
		
		int lump;
		int amt;

		int basey = 4;
		int offsety = 0;

		string basefilepath = "graphics/SevHUD/";
		string filepath = basefilepath..mapset.."/";

		// Which colon gfx to use?
		string imgcolon;
		lump = Wads.CheckNumForFullName(basefilepath..mapset.."/st_colon.png");
		if (lump >= 0)
			imgcolon = basefilepath..mapset.."/st_colon.png";
		else
			imgcolon = basefilepath.."standard/st_colon.png";
		
		// Global timer
		if (CVar.GetCVar("am_showtotaltime", CPlayer).GetBool())
		{
			int hoursoffset = 0;
			int hoursdigits = 1;
			int minsdigits = 1;
			int timeSeconds = Thinker.Tics2Seconds(Level.totaltime);
			int hours   = timeSeconds / 3600;
			int minutes = (timeSeconds % 3600) / 60;
			int seconds = timeSeconds % 60;

			int debugtime = CVar.GetCVar("sevhud_debugtime", CPlayer).GetInt();
			if (debugtime >= 1)
			{
				hours = debugtime;
				minutes = debugtime;
			}

			// hours
			if (hours >= 1)
			{
				if (hours >= 10)
					hoursdigits = 2;

				if (hours >= 100)
					hoursdigits = 3;

				hoursoffset = 1;
				amt = hours;
				DrawString(fnt, FormatNumber(amt, hoursdigits, format: 2), (-29, basey), DI_TEXT_ALIGN_RIGHT);
				DrawImage(imgcolon, (-28, basey + 1), DI_ITEM_OFFSETS);
			}

			// minutes
			if ((minutes >= 10) || (hours >= 1))
				minsdigits = 2;
			amt = minutes;
			DrawString(fnt, FormatNumber(amt, minsdigits, format: 2), (hoursoffset-19, basey), DI_TEXT_ALIGN_RIGHT);

			DrawImage(imgcolon, (-17, basey + 1), DI_ITEM_OFFSETS);

			// seconds
			amt = seconds;
			DrawString(fnt, FormatNumber(amt, 2, format: 2), (-7, basey), DI_TEXT_ALIGN_RIGHT);
			
			// move the level timer down
			offsety += fontHeight;
		}

		// Don't draw anything else if we're already drawing the stats on the status bar
		if (CVar.GetCVar("sevhud_extra", CPlayer).GetInt())
			return;
		
		// Map time
		if (CVar.GetCVar("am_showtime", CPlayer).GetBool())
		{
			int hoursoffset = 0;
			int hoursdigits = 1;
			int minsdigits = 1;
			int timeSeconds = Thinker.Tics2Seconds(Level.time);
			int hours   = timeSeconds / 3600;
			int minutes = (timeSeconds % 3600) / 60;
			int seconds = timeSeconds % 60;

			string basefilepath = "graphics/SevHUD/";
			string filepath = basefilepath..mapset.."/";

			int debugtime = CVar.GetCVar("sevhud_debugtime", CPlayer).GetInt();
			if (debugtime >= 1)
			{
				hours = debugtime;
				minutes = debugtime;
			}

			// hours
			if (hours >= 1)
			{
				if (hours >= 10)
					hoursdigits = 2;

				if (hours >= 100)
					hoursdigits = 3;

				hoursoffset = 1;
				amt = hours;
				DrawString(fnt, FormatNumber(amt, hoursdigits, format: 2), (-29, basey + offsety), DI_TEXT_ALIGN_RIGHT);
				DrawImage(imgcolon, (-28, basey + offsety + 1), DI_ITEM_OFFSETS);
			}

			// minutes
			if ((minutes >= 10) || (hours >= 1))
				minsdigits = 2;
			amt = minutes;
			DrawString(fnt, FormatNumber(amt, minsdigits, format: 2), (hoursoffset-19, basey + offsety), DI_TEXT_ALIGN_RIGHT);

			DrawImage(imgcolon, (-17, basey + offsety + 1), DI_ITEM_OFFSETS);

			// seconds
			amt = seconds;
			DrawString(fnt, FormatNumber(amt, 2, format: 2), (-7, basey + offsety), DI_TEXT_ALIGN_RIGHT);
		}

		int pcnt;
		basey = -33;
		offsety = 0;

		if (CVar.GetCVar("am_showsecrets", CPlayer).GetBool())
		{
			// Which secrets gfx to use?
			string imgsecret;
			lump = Wads.CheckNumForFullName(basefilepath..mapset.."/st_secret.png");
			if (lump >= 0)
				imgsecret = basefilepath..mapset.."/st_secret.png";
			else
				imgsecret = basefilepath.."standard/st_secret.png";

			// Draw automap secrets
			DrawImage(imgsecret, (-48, basey-fontHeight), DI_ITEM_OFFSETS);
			pcnt = Level.total_secrets ? 100 * Level.found_secrets / Level.total_secrets : 100;
			amt = multiplayer ? CPlayer.secretcount : pcnt;
			DrawString(fnt, FormatNumber(amt, 3), (-16, basey-fontHeight), DI_TEXT_ALIGN_RIGHT);

			offsety -= fontHeight;
		}

		if (CVar.GetCVar("am_showitems", CPlayer).GetBool())
		{
			// Which items gfx to use?
			string imgitem;
			lump = Wads.CheckNumForFullName(basefilepath..mapset.."/st_item.png");
			if (lump >= 0)
				imgitem = basefilepath..mapset.."/st_item.png";
			else
				imgitem = basefilepath.."standard/st_item.png";

			// Draw automap items
			DrawImage(imgitem, (-48, offsety+basey-fontHeight), DI_ITEM_OFFSETS);
			pcnt = Level.total_items ? 100 * Level.found_items / Level.total_items : 100;
			amt = multiplayer ? CPlayer.itemcount : pcnt;
			DrawString(fnt, FormatNumber(amt, 3), (-16, offsety+basey-fontHeight), DI_TEXT_ALIGN_RIGHT);

			offsety -= fontHeight;
		}

		if (CVar.GetCVar("am_showmonsters", CPlayer).GetBool())
		{
			// Which kills gfx to use?
			string imgkill;
			lump = Wads.CheckNumForFullName(basefilepath..mapset.."/st_kill.png");
			if (lump >= 0)
				imgkill = basefilepath..mapset.."/st_kill.png";
			else
				imgkill = basefilepath.."standard/st_kill.png";

			// Draw automap kills
			DrawImage(imgkill, (-48, offsety+basey-fontHeight), DI_ITEM_OFFSETS);
			pcnt = Level.total_monsters ? 100 * Level.killed_monsters / Level.total_monsters : 100;
			amt = multiplayer ? CPlayer.killcount : pcnt;
			DrawString(fnt, FormatNumber(amt, 3), (-16, offsety+basey-fontHeight), DI_TEXT_ALIGN_RIGHT);
		}
	}

	//===========================================================================
	//
	//
	//
	//===========================================================================

	void DrawSevHUD(int state)
	{
		GetMapSet();

		if ((CVar.GetCVar("sevhud_checksum", CPlayer).GetBool())&&(!gotchecksum))
			PrintChecksum();

		string basefilepath = "graphics/SevHUD/";
		string filepath = basefilepath..mapset.."/";
		
		int lump;

		// DrawMainBar
		if (automapactive)
		{
			// Map label/name
			HUDFont fnt = mHUDSmallFont;
			int fontHeight = fnt.mFont.GetHeight();

			string maplabel = "";
			string mapname = "";

			int am_showmaplabel = CVar.GetCVar("am_showmaplabel", CPlayer).GetInt();
			bool showmaplabel = false;
			bool showmapname = CVar.GetCVar("am_showlevelname", CPlayer).GetBool();

			if ((am_showmaplabel == 1) || (am_showmaplabel == 2) && (!(Level.clusterflags & Level.CLUSTER_HUB)))
				showmaplabel = true;

			if (showmaplabel)
			{
				maplabel = Level.MapName;
				if (showmapname)
					maplabel = maplabel..": ";
			}

			if (showmapname)
			{
				mapname = (Level.Levelname);
			}

			DrawString(fnt, maplabel.."\cj"..mapname, (160, 167-fontHeight), DI_TEXT_ALIGN_CENTER);
		}

		DrawImage(filepath.."st_bar.png", (0, 168), DI_ITEM_OFFSETS);

		// Don't draw a status bar at all in Lexicon VR
		// if (mapset == "none")
			// return;

		if (CVar.GetCVar("sevhud_extra", CPlayer).GetBool())
		{
			DrawStatusBarStats(state, basefilepath, littleNumbers);
			DrawStatusBarPowerups(basefilepath);
		}
		DrawImage("STTPRCNT", (90, 171), DI_ITEM_OFFSETS);
		DrawImage("STTPRCNT", (221, 171), DI_ITEM_OFFSETS);

		Inventory a1 = GetCurrentAmmo();
		if (a1 != null) DrawString(mHUDFont, FormatNumber(a1.Amount, 3), (44, 171), DI_TEXT_ALIGN_RIGHT|DI_NOSHADOW);
		DrawString(mHUDFont, FormatNumber(CPlayer.health, 3), (90, 171), DI_TEXT_ALIGN_RIGHT|DI_NOSHADOW);
		DrawString(mHUDFont, FormatNumber(GetArmorAmount(), 3), (221, 171), DI_TEXT_ALIGN_RIGHT|DI_NOSHADOW);

		DoomStatusBar.DrawBarKeys();

		DrawSevHUDAmmo(littleNumbers);

		if (deathmatch || teamplay)
		{
			DrawString(mHUDFont, FormatNumber(CPlayer.FragCount, 3), (138, 171), DI_TEXT_ALIGN_RIGHT);
		}

		if (multiplayer)
		{
			DrawImage("STFBANY", (143, 168), DI_ITEM_OFFSETS|DI_TRANSLATABLE);
		}

		if (CPlayer.mo.InvSel != null && !Level.NoInventoryBar)
		{
			DrawInventoryIcon(CPlayer.mo.InvSel, (160, 198), DI_DIMDEPLETED);
			if (CPlayer.mo.InvSel.Amount > 1)
			{
				DrawString(mAmountFont, FormatNumber(CPlayer.mo.InvSel.Amount), (175, 198-mIndexFont.mFont.GetHeight()), DI_TEXT_ALIGN_RIGHT, Font.CR_GOLD);
			}
		}
		else
		{
			DrawTexture(GetMugShot(5), (143, 168), DI_ITEM_OFFSETS);
		}
		if (isInventoryBarVisible())
		{
			DrawInventoryBar(diparms, (48, 169), 7, DI_ITEM_LEFT_TOP);
		}

		if (deathmatch || teamplay)
		{
			// Draw "FRAGS" text
			lump = Wads.CheckNumForFullName(filepath.."st_frag.png");
			if (lump >= 0)
				DrawImage(filepath.."st_frag.png", (104, 168), DI_ITEM_OFFSETS);
			else
				DrawImage(basefilepath.."standard/st_frag.png", (104, 168), DI_ITEM_OFFSETS);
			// Draw amount of frags
			DrawString(mHUDFont, FormatNumber(CPlayer.FragCount, 3), (138, 171), DI_TEXT_ALIGN_RIGHT);
		}
		else
		{
			// Special case for Private Stone
/*			if (LevelInfo.MapExists("STONEM1"))
			{
				int amt1, maxamt;
				[amt1, maxamt] = GetAmount("BG_ZerkCharge");
				DrawBar("BMETER1", "BMETER2", amt1, maxamt, (124, 194), 0, DI_ITEM_OFFSETS, 1.0);
				[amt1, maxamt] = GetAmount("BG_Stone_Charge");
				DrawBar("BMETER3", "BMETER4", amt1, maxamt, (124, 199), 0, DI_ITEM_OFFSETS, 1.0);
			}
			else
			{*/
				// Draw "ARMS" text
				lump = Wads.CheckNumForFullName(filepath.."st_arms.png");
				if (lump >= 0)
					DrawImage(filepath.."st_arms.png", (104, 168));
				else
					DrawImage(basefilepath.."standard/st_arms.png", (104, 168));
			//}

			// Draw weapons numbers
			if (littleNumbers == mDefaultIndexFont)
			{
				// If we're using the default font, then draw the standard numbers
				// That way we don't have to copy st_gnum*.png into every directory
				DrawImage(CPlayer.HasWeaponsInSlot(2)? "STYSNUM2" : "graphics/SevHUD/standard/st_gnum2.png", (111, 172), DI_ITEM_OFFSETS);
				DrawImage(CPlayer.HasWeaponsInSlot(3)? "STYSNUM3" : "graphics/SevHUD/standard/st_gnum3.png", (123, 172), DI_ITEM_OFFSETS);
				DrawImage(CPlayer.HasWeaponsInSlot(4)? "STYSNUM4" : "graphics/SevHUD/standard/st_gnum4.png", (135, 172), DI_ITEM_OFFSETS);
				DrawImage(CPlayer.HasWeaponsInSlot(5)? "STYSNUM5" : "graphics/SevHUD/standard/st_gnum5.png", (111, 182), DI_ITEM_OFFSETS);
				DrawImage(CPlayer.HasWeaponsInSlot(6)? "STYSNUM6" : "graphics/SevHUD/standard/st_gnum6.png", (123, 182), DI_ITEM_OFFSETS);
				DrawImage(CPlayer.HasWeaponsInSlot(7)? "STYSNUM7" : "graphics/SevHUD/standard/st_gnum7.png", (135, 182), DI_ITEM_OFFSETS);
			}
			else
			{
				DrawImage(CPlayer.HasWeaponsInSlot(2)? filepath.."st_ynum2.png" : filepath.."st_gnum2.png", (111, 172), DI_ITEM_OFFSETS);
				DrawImage(CPlayer.HasWeaponsInSlot(3)? filepath.."st_ynum3.png" : filepath.."st_gnum3.png", (123, 172), DI_ITEM_OFFSETS);
				DrawImage(CPlayer.HasWeaponsInSlot(4)? filepath.."st_ynum4.png" : filepath.."st_gnum4.png", (135, 172), DI_ITEM_OFFSETS);
				DrawImage(CPlayer.HasWeaponsInSlot(5)? filepath.."st_ynum5.png" : filepath.."st_gnum5.png", (111, 182), DI_ITEM_OFFSETS);
				DrawImage(CPlayer.HasWeaponsInSlot(6)? filepath.."st_ynum6.png" : filepath.."st_gnum6.png", (123, 182), DI_ITEM_OFFSETS);
				DrawImage(CPlayer.HasWeaponsInSlot(7)? filepath.."st_ynum7.png" : filepath.."st_gnum7.png", (135, 182), DI_ITEM_OFFSETS);
			}
		}
	}
}
